<?xml version='1.0' encoding='utf-8'?>
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
    "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
<meta name="generator" content="HTML Tidy for Linux (vers 25 March 2009), see www.w3.org" />
<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
<link href="style.css" rel="stylesheet" type="text/css" />
<title>
Modelica.Electrical.Spice3.Examples.Spice3BenchmarkFourBitBinaryAdder</title>
</head>
<body>
<h1><a href="index.html">.</a><a href="Modelica.html">Modelica</a>.<a href="Modelica.Electrical.html">Electrical</a>.<a href="Modelica.Electrical.Spice3.html">Spice3</a>.<a href="Modelica.Electrical.Spice3.Examples.html">Examples</a>.Spice3BenchmarkFourBitBinaryAdder</h1>
<h2><a name="info" id="info">Information</a></h2>
<p>This Four Bit Binary Adder model is one of the five benchmark
circuits described in the SPICE3 Version e3 User's Manual (see
information of package Spice3).</p>
<p>The model adds two 4-bit numbers. It has eight inputs where the
first one is the lowest-order bit of the first number, the second
is the lowest-order bit of the second number, the third one is the
second-order bit of first number and so on. The Four Bit Binary
Adder has four outputs where the first one (node 9) is the
lowest-order bit, the second and the third one (node 10 and node
11) are the next two bits and the last one (node 12) is the
highest-order bit. The picture illustrates the pin-assignment
referring to line 41 and 42 in the SPICE3 source code.</p>
<blockquote><img src="../../../Modelica%203.2.1/Resources/Images/Electrical/Spice3/FourBitBinaryAdder.jpg" alt="segment.png" /></blockquote>
<p>The Four Bit Binary Adder is built out of two two bit adders
which respectively are built out of two one bit adders. One one bit
adder is build out of nine NAND circuits.</p>
<p>Please note, that the simulation time of the Four Bit Binary
Adder can take several hours due to its immense size (e.g. 11387
equations).</p>
<p>The user is recommended to simulate from t=0 to t=1e-6s and
observe the eight inputs (X1.p1.v, ..., X1.p8.v) and the four
outputs (X1.p9.v, ..., X1.p12.v) and the carryout output
(X1.p14.v).</p>
<p>Original SPICE3 netlist of the Four Bit Binary Adder:</p>
<pre>
ADDER - 4 BIT ALL-NAND-GATE BINARY ADDER

*** SUBCIRCUIT DEFINITIONS
.SUBCKT NAND 1 2 3 4
*   NODES:  INPUT(2), OUTPUT, VCC
Q1        9  5  1 QMOD
D1CLAMP   0  1    DMOD
Q2        9  5  2 QMOD
D2CLAMP   0  2    DMOD
RB        4  5    4K
R1        4  6    1.6K
Q3        6  9  8 QMOD
R2        8  0    1K
RC        4  7    130
Q4        7  6 10 QMOD
DVBEDROP 10  3    DMOD
Q5        3  8  0 QMOD
.ENDS NAND

.SUBCKT ONEBIT 1 2 3 4 5 6
*   NODES:  INPUT(2), CARRY-IN, OUTPUT, CARRY-OUT, VCC
X1   1  2  7  6   NAND
X2   1  7  8  6   NAND
X3   2  7  9  6   NAND
X4   8  9 10  6   NAND
X5   3 10 11  6   NAND
X6   3 11 12  6   NAND
X7  10 11 13  6   NAND
X8  12 13  4  6   NAND
X9  11  7  5  6   NAND
.ENDS ONEBIT

.SUBCKT TWOBIT 1 2 3 4 5 6 7 8 9
*   NODES:  INPUT - BIT0(2) / BIT1(2), OUTPUT - BIT0 / BIT1,
*           CARRY-IN, CARRY-OUT, VCC
X1   1  2  7  5 10  9   ONEBIT
X2   3  4 10  6  8  9   ONEBIT
.ENDS TWOBIT

.SUBCKT FOURBIT 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15
*   NODES:  INPUT - BIT0(2) / BIT1(2) / BIT2(2) / BIT3(2),
*           OUTPUT - BIT0 / BIT1 / BIT2 / BIT3, CARRY-IN, CARRY-OUT, VCC
X1   1  2  3  4  9 10 13 16 15   TWOBIT
X2   5  6  7  8 11 12 16 14 15   TWOBIT
.ENDS FOURBIT

*** DEFINE NOMINAL CIRCUIT
.MODEL DMOD D
.MODEL QMOD NPN(BF=75 RB=100 CJE=1PF CJC=3PF)
VCC   99  0   DC 5V
VIN1A  1  0   PULSE(0 3 0 10NS 10NS   10NS   50NS)
VIN1B  2  0   PULSE(0 3 0 10NS 10NS   20NS  100NS)
VIN2A  3  0   PULSE(0 3 0 10NS 10NS   40NS  200NS)
VIN2B  4  0   PULSE(0 3 0 10NS 10NS   80NS  400NS)
VIN3A  5  0   PULSE(0 3 0 10NS 10NS  160NS  800NS)
VIN3B  6  0   PULSE(0 3 0 10NS 10NS  320NS 1600NS)
VIN4A  7  0   PULSE(0 3 0 10NS 10NS  640NS 3200NS)
VIN4B  8  0   PULSE(0 3 0 10NS 10NS 1280NS 6400NS)
X1     1  2  3  4  5  6  7  8  9 10 11 12  0 13 99 FOURBIT
RBIT0  9  0   1K
RBIT1 10  0   1K
RBIT2 11  0   1K
RBIT3 12  0   1K
RCOUT 13  0   1K

*** (FOR THOSE WITH MONEY (AND MEMORY) TO BURN)
.TRAN 1NS 6400NS UIC

.control
run
set options no break

*plot v(1) v(2)
*plot v(3) v(4)
*plot v(5) v(6)
*plot v(7) v(8)
*plot v(9) v(10)
*plot v(11) v(12)
*plot v(13)
*print v(9) v(10)
print v(11) v(12) v(13)

.endc

.END
</pre>
<p>The model is built out of several subcircuits which were
described only ones and used several times.</p>
<h2>Contents</h2>
<table summary="Items">
<tr>
<th>Name</th>
<th>Description</th>
</tr>
<tr>
<td><a href="Modelica.Electrical.Spice3.Examples.Spice3BenchmarkFourBitBinaryAdder.NAND.html">
NAND</a></td>
<td>.SUBCKT NAND 1 2 3 4</td>
</tr>
<tr>
<td><a href="Modelica.Electrical.Spice3.Examples.Spice3BenchmarkFourBitBinaryAdder.ONEBIT.html">
ONEBIT</a></td>
<td>.SUBCKT ONEBIT 1 2 3 4 5 6</td>
</tr>
<tr>
<td><a href="Modelica.Electrical.Spice3.Examples.Spice3BenchmarkFourBitBinaryAdder.TWOBIT.html">
TWOBIT</a></td>
<td>.SUBCKT TWOBIT 1 2 3 4 5 6 7 8 9</td>
</tr>
<tr>
<td><a href="Modelica.Electrical.Spice3.Examples.Spice3BenchmarkFourBitBinaryAdder.FOURBIT.html">
FOURBIT</a></td>
<td>.SUBCKT FOURBIT 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15</td>
</tr>
</table>
<hr />
Generated at 2013-08-23T06:45:12Z by <a href="http://openmodelica.org">OpenModelica</a>1.9.0 beta4+dev (r16855)
</body>
</html>
